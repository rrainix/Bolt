cmake_minimum_required(VERSION 3.23)

project(BoltCore LANGUAGES CXX)


set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# === Quellen & Public-Header erfassen ===
file(GLOB_RECURSE CORE_SRCS    "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp")
file(GLOB_RECURSE CORE_HEADERS "${CMAKE_CURRENT_SOURCE_DIR}/include/*.hpp")


add_library(BoltCore STATIC ${CORE_SRCS})


target_include_directories(BoltCore
  PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
    $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/Vendor/External/Include/>
  PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/src
)

target_precompile_headers(BoltCore PRIVATE
  "${CMAKE_CURRENT_SOURCE_DIR}/include/Bolt/btpch.hpp")


if(MSVC)
  target_compile_options(BoltCore PRIVATE /Zi)
endif()


link_directories("${CMAKE_SOURCE_DIR}/Vendor/External/Library")
file(GLOB EXTERNAL_LIBS "${CMAKE_SOURCE_DIR}/Vendor/External/Library/*.lib")
target_link_libraries(BoltCore PUBLIC ${EXTERNAL_LIBS})


target_sources(BoltCore
  PUBLIC
    FILE_SET HEADERS
    BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/include
    FILES     ${CORE_HEADERS}
)


source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR}/include FILES ${CORE_HEADERS})
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR}/src     FILES ${CORE_SRCS})


install(TARGETS BoltCore
  EXPORT BoltCoreTargets
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  FILE_SET HEADERS DESTINATION include
)
install(EXPORT BoltCoreTargets
  FILE BoltCoreConfig.cmake
  NAMESPACE BoltCore::
  DESTINATION lib/cmake/BoltCore
)
install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/include/ DESTINATION include)
